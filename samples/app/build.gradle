buildscript {
    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.0.4'
    }
}

//Load API Key information in from property file
Properties props = new Properties()
try {
    props.load(file('twitter.properties').newDataInputStream())
} catch (Exception ignored) {
    throw new GradleException("Missing twitter.properties, check the twitter.properties.sample file.")
}

apply plugin: 'com.android.application'
apply plugin: 'spoon'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.example.app.twitter"
        versionCode 1
        versionName "1.0"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    buildTypes {
        debug {
            resValue "string", "com.twitter.sdk.android.CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            resValue "string", "com.twitter.sdk.android.CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            resValue "bool", "com.twitter.sdk.android.TRACE_ENABLED", "true"
            resValue "bool", "com.twitter.sdk.android.COLLECT_IDENTIFIERS_ENABLED", "true"
            buildConfigField "String", "MOPUB_AD_UNIT_ID", "\"${props.getProperty("mopubAdUnitId")}\""
        }
        release {
            resValue "string", "com.twitter.sdk.android.CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            resValue "string", "com.twitter.sdk.android.CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            resValue "bool", "com.twitter.sdk.android.TRACE_ENABLED", "false"
            resValue "bool", "com.twitter.sdk.android.COLLECT_IDENTIFIERS_ENABLED", "true"
            buildConfigField "String", "MOPUB_AD_UNIT_ID", "\"${props.getProperty("mopubAdUnitId")}\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    spoon {
        if (project.hasProperty('spoonClassName')) {
            className = project.spoonClassName
        }
    }

    lintOptions {
        warningsAsErrors true
    }

    compileOptions {
        sourceCompatibility rootProject.sourceCompatibilityVersion
        targetCompatibility rootProject.targetCompatibilityVersion
    }
}

dependencies {
    implementation project(':twitter')
    implementation project(':twitter-mopub')
    implementation 'androidx.appcompat:appcompat:1.2.0'
//    implementation dependency.appCompat
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation "com.google.android.material:material:1.3.0"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.3'
//
//    androidTestCompile dependency.spoon
//    androidTestCompile dependency.espresso
//    androidTestCompile dependency.androidTestingSupport
}
